apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId 'ua.sergeimunovarov.tcalc'
        minSdkVersion 15
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 23
        versionName '4.1'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    testOptions {
        animationsDisabled true // Works with API 24+ ?
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false

            buildConfigField "boolean", "SHOULD_CLEAR_PREFS_ON_START", "false"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    retrolambda {
        //noinspection GroovyAssignabilityCheck
        javaVersion JavaVersion.VERSION_1_7
        defaultMethods false
        incremental true
    }

    dexOptions {
        javaMaxHeapSize '4g'
        preDexLibraries true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

ext {
    supportVersion = '25.4.0'
    daggerVersion = '2.11'
    autoValueVersion = '1.5.1'

    leakCanaryVersion = '1.5.1'

    assertjVersion = '2.8.0'
    mockitoVersion = '2.9.0'
    espressoVersion = '3.0.1'
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
    resolutionStrategy.force "com.android.support:support-compat:$supportVersion"
    resolutionStrategy.force "com.android.support:support-v4:$supportVersion"
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"

    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile "org.mockito:mockito-android:$mockitoVersion"
    androidTestCompile "org.assertj:assertj-core:$assertjVersion"
    androidTestCompile 'com.github.fabioCollini:DaggerMock:0.7.0'
    androidTestCompile 'com.bartoszlipinski:cortado:1.2.0'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    compile 'com.google.android.gms:play-services-oss-licenses:11.4.0'

    compile "com.android.support:support-annotations:$supportVersion"
    compile "com.android.support:support-core-utils:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    provided "com.google.auto.value:auto-value:$autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
}

apply from: '../tools/checkstyle.gradle'
