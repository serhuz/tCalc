apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'org.ajoberstar.grgit'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def fabricPropertiesFile = rootProject.file("fabric.properties")
def fabricProperties = new Properties()
fabricProperties.load(new FileInputStream(fabricPropertiesFile))

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId 'ua.sergeimunovarov.tcalc'
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 36
        versionName '5.0.1'

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        def addPreferenceKeyConstant = { keyName, keyValue ->
            buildConfigField "String", "${keyName.toUpperCase()}", "\"$keyValue\""
            resValue "string", "$keyName", "\"$keyValue\""
        }

        addPreferenceKeyConstant('key_recalculate', 'recalculate_auto')

        buildConfigField "String", "DB_NAME", "\"tcalc_room.db\""
        buildConfigField "String", "PRIVACY_POLICY_URL", "\"https://serhuz.github.io/tCalc/privacy\""

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true // Works with API 24+ ?
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false

            buildConfigField "boolean", "SHOULD_CLEAR_PREFS_ON_START", "false"
        }

        candidate {
            initWith(release)
            signingConfig signingConfigs.release
            minifyEnabled false
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {

        beta {
            manifestPlaceholders << [FABRIC_API_KEY: fabricProperties['apiKey']]

            versionNameSuffix "-beta-${grgit.head().getAbbreviatedId()}"
        }

        stable {
            ext.enableCrashlytics = false
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def buildType = variant.buildType.name
        if (names.contains("beta") && buildType != 'release') {
            variant.ignore = true
        }
    }

//    testBuildType 'candidate'

    gradle.taskGraph.useFilter { task ->
        if (task.name.endsWith('UnitTest') && (task.name.contains('Candidate') || task.name.contains('Release'))) {
            return false
        }
        return true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '4g'
        preDexLibraries true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

ext {
    supportVersion = '28.0.0'
    daggerVersion = '2.17'
    autoValueVersion = '1.6.2'
    archVersion = '1.1.1'
    roomVersion = '1.1.1'

    leakCanaryVersion = '1.6.1'

    assertjVersion = '2.9.1'
    mockitoVersion = '2.22.0'
    espressoVersion = '3.0.2'
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
    resolutionStrategy.force "com.android.support:support-compat:$supportVersion"
    resolutionStrategy.force "com.android.support:support-v4:$supportVersion"
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0-alpha4'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test:rules:1.1.0-alpha4'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "org.assertj:assertj-core:$assertjVersion"
    androidTestImplementation 'com.github.fabioCollini:DaggerMock:0.8.4'
    androidTestImplementation 'com.bartoszlipinski:cortado:1.2.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.0'
    androidTestImplementation 'androidx.room:room-testing:2.0.0-rc01'
    androidTestUtil 'androidx.test:orchestrator:1.1.0-alpha4'

    betaImplementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.0'

    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0'

    implementation 'androidx.room:room-runtime:2.0.0-rc01'
    annotationProcessor 'androidx.room:room-compiler:2.0.0-rc01'

    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    compileOnly "com.google.auto.value:auto-value-annotations:$autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'
}

apply from: '../tools/checkstyle.gradle'
