apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'com.getkeepsafe.dexcount'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.3'

    defaultConfig {
        applicationId 'ua.sergeimunovarov.tcalc'
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 25
        versionName '4.2.1'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    testOptions {
        animationsDisabled true // Works with API 24+ ?
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false

            buildConfigField "boolean", "SHOULD_CLEAR_PREFS_ON_START", "false"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'default'

    productFlavors {
        app {

        }

        screenshots {
            sourceSets {
                androidTest {
                    java.srcDirs = ['src/androidTestScreenshots/java']
                }
                test {
                    java.srcDirs = []
                }
            }

            applicationIdSuffix '.screenshots'
            versionNameSuffix '-screens'
        }
    }

    variantFilter { variant ->
        def flavorNames = variant.flavors*.name
        def buildTypes = variant.buildType*.name
        if (flavorNames.contains('screenshots') && buildTypes.contains('release')) {
            setIgnore true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '4g'
        preDexLibraries true
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

ext {
    supportVersion = '26.1.0'
    daggerVersion = '2.12'
    autoValueVersion = '1.5.2'

    leakCanaryVersion = '1.5.4'

    assertjVersion = '2.8.0'
    mockitoVersion = '2.13.0'
    espressoVersion = '3.0.1'
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
    resolutionStrategy.force "com.android.support:support-compat:$supportVersion"
    resolutionStrategy.force "com.android.support:support-v4:$supportVersion"
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "org.assertj:assertj-core:$assertjVersion"
    androidTestImplementation 'com.github.fabioCollini:DaggerMock:0.8.2'
    androidTestImplementation 'com.bartoszlipinski:cortado:1.2.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    implementation 'com.google.android.gms:play-services-oss-licenses:11.6.2'

    implementation "com.android.support:support-annotations:$supportVersion"
    implementation "com.android.support:support-core-utils:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    compileOnly "com.google.auto.value:auto-value:$autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'
}

apply from: '../tools/checkstyle.gradle'
